// $Id: standard.lib,v 1.2 1997-07-11 11:12:46 obachman Exp $
///////////////////////////////////////////////////////////////////////////////

LIBRARY: standard.lib	PROCEDURES WHICH ARE ALWAYS LOADED AT START-UP

 stdfglm(ideal[,ord])   standard basis of the ideal via fglm [and ordering ord]
///////////////////////////////////////////////////////////////////////////////

proc stdfglm (ideal i, list #)
USAGE:   stdfglm(i[,s]); i ideal, s string (any allowed ordstr of a ring)
RETURN:  stdfglm(i);    standard basis of i in the given ring, calculated via 
			fglm from ordering "dp" to the current ordering.
	 stdfglm(i,s);  standard basis of i in the given ring, calculated via
		        fglm from ordering s (as a string) to the given 
			ordering.
EXAMPLE: example stdfglm; shows an example
{
   string os;
   def dr= basering;
   if( (size(#)==0) or (typeof(#[1]) != "string") ) 
   {
     os = "dp(" + string( nvars(dr) ) + ")";
     if ( (find( ordstr(dr), os ) != 0) and (find( ordstr(dr), "a") == 0) ) 
     {
       os= "Dp";
     } 
     else 
     {
       os= "dp";
     }
   }
   else { os = #[1]; }
   execute "ring sr="+charstr(dr)+",("+varstr(dr)+"),"+os+";";
   ideal i= fetch(dr,i);
   intvec opt= option(get);
   option(redSB);
   i=std(i);
   option(set,opt);
   setring dr;
   return (fglm(sr,i));
}
example
{ "EXAMPLE:"; echo = 2;
   ring r=0,(x,y,z), lp; ideal i=y3+x2, x2y+x2, x3-x2, z4-x2-y;
   ideal is=stdfglm(i,"Dp"); is;
}
///////////////////////////////////////////////////////////////////////////////
